language java

// All core stdlib functions can be done here
private pattern before_each_file_stdlib() {
	file($body) where {
		$body <: init_vars(),
		$body <: maybe contains bubble import_declaration() as $this_import where {
			$GLOBAL_EXIST_IMPORTS += $this_import
		}
	}
}

private pattern after_each_file_stdlib() {
	after_rewrite(),
	after_each_file_global_rewrites()
}

// These could be redefined in the future (not presently supported)
pattern before_each_file() { before_each_file_stdlib() }

pattern after_each_file() { after_each_file_stdlib() }

private pattern after_rewrite() {
	file($body) where { $body <: maybe process_all_import() }
}

private pattern init_vars() {
	$_ where {
		$GLOBAL_EXIST_IMPORTS = [],
		$GLOBAL_NEW_FROM_IMPORTS = [],
		$GLOBAL_REMOVE_IMPORTS = [],
		$GLOABL_REMOVE_DUPLICATE_EXIST_IMPORTS = false
	}
}

private pattern process_all_import() {
    $body where {
            // short-circuit if no changes
            or {
                $GLOBAL_NEW_FROM_IMPORTS <: not [],
                $GLOBAL_REMOVE_IMPORTS <: not [],
                $GLOBAL_REMOVE_DUPLICATE_EXIST_IMPORTS <: true,
            },
            $last_import = "",
            $exists_imports = [],
            $body <: contains package_declaration() as $pkg_decl,
            // no existing imports, just add all new imports
            $all_import = join(distinct($GLOBAL_NEW_FROM_IMPORTS), `\n`),
            if ($body <: not contains bubble() import_declaration()) {
                if ($all_import <: not ``) {
                    $pkg_decl => `$pkg_decl\n\n$all_import`
                }
            }
            else {
                $body <: contains bubble($last_import, $exists_imports) import_declaration() as $this_import where {
                    $last_import = $this_import,
                    if ($exists_imports <: some $this_import) {
                        // duplicate import, remove it
                        if ($GLOBAL_REMOVE_DUPLICATE_EXIST_IMPORTS <: true ) {
                            $this_import => .
                        }
                    } else {
                        if ($GLOBAL_REMOVE_IMPORTS <: some $this_import) {
                            $this_import => .
                        },
                        $exists_imports += text($this_import)
                    }
                },
                // last_import need to be deleted if in remove imports
                if ($GLOBAL_REMOVE_IMPORTS <: some $last_import) {
                    $last_import => `$all_import`
                } else if ($last_import <: not ""){
                    if ($all_import <: not ``) {
                        $last_import => `$last_import\n$all_import`
                    }
                }
            }
    }
}
